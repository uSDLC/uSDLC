$ ->
  ###
  Called by form to create a new section below the current one. It will create
  a new header link pointing to a child page based on the title.
  ###
  usdlc.newPageSection = (type) ->
    form = new usdlc.FormProcessor "#new#{type}Section"
    title = form.fieldMap['title']
    href = usdlc.camelCase(title)
    href = "#{type}_#{href}" if type isnt 'General'
    id = usdlc.nextSectionId()
    a = usdlc.createLink href, 'page', "#{id}a0", title
    insertNewSection form, a, 2, id, type
    return false
  ###
  Called by form to create a new section below the current one. It will create
  a new header link referencing an instrumentation script based on the title.
  ###
  usdlc.newScenarioSection = ->
    newCodeSection 'Scenario', createBddLink
  usdlc.newInstrumentationSection = ->
    newCodeSection 'Instrumentation', createSingleLink
  usdlc.newReferenceSection = ->
    newCodeSection 'Reference', createSingleLink, 'reference'

  newCodeSection = (name, codeLinkCreator, type = 'runnable') ->
    form = new usdlc.FormProcessor "#new#{name}Section"
    [title, ext] = usdlc.titleExt form, 'title'
    href = "#{usdlc.camelCase(title)}.#{ext}"
    id = usdlc.nextSectionId()
    a = codeLinkCreator form, href, type, "#{id}a0", title
    insertNewSection form, a, 2, id, name
    return false


  usdlc.titleExt = (form, titleFieldName) ->
    title = form.fieldMap[titleFieldName]
    split = /^(.*)\.(\w+)$/.exec(title)
    if split
      title = split[1]
      ext = split[2]
      addActorType(ext)
    else
      ext = form.fieldMap['type']
    usdlc.setCookie('actorDefault', ext)
    return [title, ext]

  createBddLink = (form, href, action, id, title) ->
    $.get '/usdlc/support/usdlc/newScenario.groovy',
      scriptName: href
      page: usdlc.pageContentsURL
      given: form.fieldMap['Given']
      when: form.fieldMap['When']
      then: form.fieldMap['Then'], ->
        usdlc.absolutePageContents usdlc.pageContentsURL
    heading = $ '<span/>'
    heading.html title
    gwt = $ '<span/>', class: 'givenWhenThen'
    gwt.append(' &nbsp;(')
    gwt.append usdlc.createLink "Given_#{href}", 'runnable', "#{id}g", 'G'
    gwt.append usdlc.createLink "When_#{href}", 'runnable', "#{id}w", 'W'
    gwt.append usdlc.createLink "Then_#{href}", 'runnable', "#{id}t", 'T'
    gwt.append(')')
    heading.append gwt
    return heading

  createSingleLink = (form, href, action, id, title) ->
    usdlc.createLink href, action, id, title
  ###
  Called by form to create a new section below the current one. It will create
  a new header link pointing to a child page based on the title.
  ###
  usdlc.newGeneralSection = (type) ->
    form = new usdlc.FormProcessor "#new#{usdlc.camelCase(type)}Section"
    title = form.fieldMap['title']
    id = usdlc.nextSectionId()
    heading = $ '<span/>'
    heading.html title
    if insertNewSection form, heading, 2, id, type
      usdlc.editSectionInFocus()
    return false
  ###
  This is where the work is done...
  ###
  usdlc.createLink = (href, action, id, title) ->
    a = $ '<a/>',
      href: href
      action: action
      id: id
      class: 'usdlc'
    a.html title
    return a
  ###
  Create a new section given a template
  ###
#  usdlc.sectionTemplate = (name, page = 'Sections') ->
#    usdlc.loadSection name, page, (h1) ->
#      section = usdlc.newSection(name)
#      section.append h1.nextAll()
#      section.insertAfter(usdlc.inFocus)
#      usdlc.setFocus(section)
#      usdlc.editSectionInFocus()

  insertNewSection = (form, heading, level, id, type) ->
    edit = false
    if usdlc.inFocus
      notes = form.fieldMap.notes

      section = usdlc.newSection type, id
      section.addClass('page') if heading.is('a')
      h1 = $ "<h#{level}/>"
      h1.append heading

      section.append h1
      if notes then section.append $ "<p>#{notes}</p>"
      section.insertAfter(usdlc.inFocus)
      usdlc.setFocus(section)
      usdlc.savePage()
      edit = not notes
    else
      usdlc.alert "Select Section First"

    if form.fieldMap.KeepOpen
      edit = false
    else
      usdlc.closeDialog()

    return edit
  ###
  Creates basic new section code.
  ###
  usdlc.newSection = (type = '', id = usdlc.nextSectionId()) ->
    div = $ "<div/>",
      id : id
      'class' : "editable section synopsis #{type}"
    if type
      div.append "<div class='sectionType'>#{type}</div>"
    return div

  usdlc.loadActorTypes = (select) ->
    actorTypes = (usdlc.cookie('actorTypes') || 'coffee').split(',')
    $.each actorTypes, (index, value) ->
      option = $ '<option/>',
        value: value
      option.text value
      select.append option
    select.val usdlc.cookie("actorDefault") || 'coffee'

  addActorType = (ext) ->
    actorTypes = (usdlc.cookie('actorTypes') || 'coffee').split(',')
    dups = (type for type in actorTypes when type == ext)
    if dups.length == 0
      actorTypes.push(ext)
      actorTypes.sort()
      usdlc.setCookie('actorTypes', actorTypes.toString())

  findType = (url) ->
    m = /(.*)\/(?:([a-zA-Z0-9]+)_)?([a-zA-Z0-9]+)(?:\/index\.html)?$/.exec url
    return m?[2] ? ''
    #return {path: m[1], type: m[2], name: m[3]}

  usdlc.newSectionForm = ->
    type = findType(usdlc.pageContentsURL)
    usdlc.form 'New Section:', (tabs) ->
      try
        li = $("a:contains('#{type}')", tabs).parent()
        next = li.next()
        li = next if next.size()
        href = $('a', li).attr('href')
        return href.substring(1)
      catch e
        return null

  usdlc.loadScenario = (input) ->
    return if not usdlc.inDialog(input)
    data =
      page: usdlc.pageContentsURL
      gwt: input.attr('name')
    $.get '/usdlc/support/usdlc/loadScenario.groovy', data, (r)->mac(input, r)

  usdlc.loadProjects = (input) ->
    $.get '/usdlc/support/usdlc/loadProjects.groovy',
    unassigned: true, (r)->sac(input, r)

  usdlc.newProject = ->
    form = new usdlc.FormProcessor "#newProject"
    title = form.fieldMap['title']
    usdlc.closeDialog()
    $.get '/usdlc/support/usdlc/newProject.groovy', project:title, (rslt) ->
        usdlc.contentTree.jstree 'refresh', -1
    return false

  usdlc.loadUsdlcProjects = (input) ->
    $.get '/usdlc/support/usdlc/loadProjects.groovy', (r)->sac(input, r)

  usdlc.createNewUser = ->
    form = new usdlc.FormProcessor "#createNewUser"
    usdlc.closeDialog() if not form.fieldMap.keepOpen
    $.get '/usdlc/support/usdlc/newUser.groovy', form.fieldMap, (rslt) ->
      if rslt is 'ok'
        usdlc.contentTree.jstree 'refresh', -1
      else
        usdlc.highlight('red')
        alert(rslt)
    return false

  usdlc.loadGroups = (input) ->
    $.get '/usdlc/support/usdlc/loadGroups.groovy', (r)->mac(input, r)

  sac = (input, response) -> # single auto-complete
    choices = eval response
    input.autocomplete
      source: choices
      minLength: 1

  mac = (input, response) -> # multi-select auto-complete
    input.bind "keydown", (event) ->
      # don't navigate away from the field on tab when selecting an item
      if event.keyCode is $.ui.keyCode.TAB
    	  if $(this).data("autocomplete").menu.active
    		  event.preventDefault()
    choices = eval response
    input.autocomplete
      minLength: 1
      autoFocus: true
      source: (request, response) ->
        # delegate back to autocomplete, but extract the last term
        last = request.term.split(/,\s*/).pop()
        response  $.ui.autocomplete.filter choices, last
      focus: -> return false # prevent value inserted on focus
      select: (event, ui) ->
        terms = this.value.split /,\s*/
        terms.pop() # remove the current input
        terms.push ui.item.value # add the selected item
		  # add placeholder to get the comma-and-space at the end
        terms.push ''
        this.value = terms.join ', '
        return false
