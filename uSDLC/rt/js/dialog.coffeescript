$ ->
  ###
  Create a dialog box to wrap an existing item. Options are those for
  jquery dialog, but height can be a percentage as well as a pixel
  count
  ###
  percentRE = /^(\d+)%$/

  openDialogs = []

  pageRelativeDialog = (box) ->
    box.dialog("widget").position(
        of: $('body')
        my: 'center bottom'
        at: 'center center'
    )

  sectionRelativeDialog = (box) ->
    if usdlc.inFocus and not openDialogs.length
      box.dialog("widget").position(
          of: usdlc.inFocus
          my: 'top'
          at: 'bottom'
          collision:'flip'
      )
    else
      pageRelativeDialog(box)

  usdlc.dialog = (contents, options) ->
    contents = $(contents)
    options ?= {}
    match = options.height?.match percentRE
    if match
        percent = parseInt(match[1])
        options.height = $(window).height() * percent / 10.0

    contents.dialog options
    # dialog sets iframe width to auto - which does not fill the parent
    contents.css 'width', '98%'
    return contents

  usdlc.alert = (name) ->
    usdlc.highlight('red')
    showDialog name, 'Alerts', (box) ->
      box.dialog 'option', 'dialogClass', 'ui-state-error ui-corner-all'
      sectionRelativeDialog(box)

  usdlc.closeDialog = ->
    if openDialogs.length
      box = openDialogs.pop()
      box.dialog("destroy").detach()

  usdlc.inDialog = (element) ->
    return element.parents('.ui-dialog').size() > 0

  showDialog = (name, page, after) ->
    split = name.split(':')
    if split.length > 1
      page = "#{page}/#{usdlc.camelCase(split[0])}"
    usdlc.loadSection name, page, (h1) ->
      options =
        dialogClass: 'ui-corner-all'
        modal:      true
        minHeight:  100
        height:     'auto'
        width: 'auto'
        position: [0,10000]
        close: -> usdlc.closeDialog()

      box = usdlc.dialog("<div/>", options)
      if h1.size() == 1
        title = single box, h1
      else
        title = tabbed box, h1
      usdlc.activateHtml(box)
      openDialogs.push box

      box.dialog("option", "title", title)
      $('.editable', box).removeClass('editable')
      after box

  single = (box, heading) ->
    html = heading.nextAll()
    box.append html
    return getTitle(html, heading)[0]

  tabno = 0

  tabbed = (box, headings) ->
    tabPanel = $ '<div/>', class:"tab-container"
    box.append tabPanel
    box.tabPanel = tabPanel
    tabs = $ '<ul/>', class:"etabs"
    tabPanel.append(tabs)
    title = ''
    headings.each (index) ->
      heading = $(this)
      html = heading.nextAll()
      [title, tabName] = getTitle(html, heading)
      id = "tabs-#{tabno++}"
      tab = $ '<li/>', class:"tab"
      a = $ '<a/>', href: '#' + id
      a.text tabName
      tab.append a
      tabs.append tab
      body = $ '<div/>', id: id
      body.append heading.nextAll()
      tabPanel.append body
    tabPanel.easytabs animate: false, updateHash: false
    return title

  getTitle = (box, heading) ->
    h1 = $("h1", box)
    if h1.length
        title = h1.first().text()
        h1.detach()
    else
        title = heading.text()
    return title.split(':')
  #
  # Display a form. If the form has variants (in tabs), use the optional
  # tabSelector closure to return the id of the tab (being the li, a or
  # panel id.
  #
  usdlc.form = (name, tabSelector) ->
    showDialog name, 'Forms', (box) ->
      pageRelativeDialog(box)
      active = box
      if tabSelector and box.tabPanel
        tabId = tabSelector(box.tabPanel)
        if tabId
          box.tabPanel.easytabs('select', tabId)
          active = $('div.active', box)
      # jqueryui dialog does not render properly if focus set too early
      complete = -> $('input', active).first().focus()
      setTimeout complete, 300

  usdlc.tabNames = (tabs) ->
    map = {}
    $('ul.etabs a').each (i, a) ->
      map[$.trim(a.text())] = a.parent()
    return map

  class FormProcessor
    constructor: (selector) ->
      @form = $ selector
      @fieldMap = {}
      @altMap = {}
      @fieldList = []
      self = this
      $.each @form.serializeArray(), (i, field) =>
        @fieldMap[field.name] = field.value
        alt = $("[name=#{field.name}]", @form).attr('alt')
        @altMap[field.name] = alt ? ''
        @fieldList.push field.name

    toHtml: ->
      html = []
      for name in @fieldList
        value = @fieldMap[name].replace(/\n/g, '<br>')
        if value.length
          label = @altMap[name]
          if label
            html.push "<b class='blue'>#{label}</b> #{value}"
          else
            html.push value
      return '' if not html.length
      return "<div>#{html.join('<br/>')}</div>"

  usdlc.FormProcessor = FormProcessor
