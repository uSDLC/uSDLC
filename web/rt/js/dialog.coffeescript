$ ->
  ###
  Create a dialog box to wrap an existing item. Options are those for
  jquery dialog, but height can be a percentage as well as a pixel
  count
  ###
  dialogPath = '/uSDLC/Environment/Configuration/DialogBoxes'
  percentRE = /^(\d+)%$/

  openDialogs = []

  pageRelativeDialog = (box) ->
    box.dialog("widget").position(
        of: $('body')
        my: 'center bottom'
        at: 'center center'
    )

  sectionRelativeDialog = (box) ->
    if usdlc.inFocus and not openDialogs.length
      box.dialog("widget").position(
          of: usdlc.inFocus
          my: 'top'
          at: 'bottom'
          collision:'flip'
      )
    else
      pageRelativeDialog(box)

  usdlc.dialog = (contents, options) ->
    contents = $(contents)
    options ?= {}
    match = options.height?.match percentRE
    if match
        percent = parseInt(match[1])
        options.height = $(window).height() * percent / 10.0

    contents.dialog options
    # dialog sets iframe width to auto - which does not fill the parent
    contents.css 'width', '98%'
    return contents

  usdlc.alert = (name) ->
    usdlc.highlight('red')
    showDialog name, 'Alerts', (box) ->
      box.dialog 'option', 'dialogClass', 'ui-state-error ui-corner-all'
      sectionRelativeDialog(box)

  usdlc.form = (name) ->
    showDialog name, 'Forms', (box) ->
      pageRelativeDialog(box)
      # jqueryui dialog does not render properly if focus set too early
      complete = -> $('input', box).first().focus()
      setTimeout complete, 300

  usdlc.closeDialog = ->
    if openDialogs.length
      box = openDialogs.pop()
      box.dialog("destroy").detach()

  showDialog = (name, page, after) ->
    split = name.split('#')
    if split.length > 1 then [page,name] = split
    first = page[0]
    if first isnt '~' and first isnt '/'
      page = "#{dialogPath}/#{page}"

    $.get usdlc.serverActionUrl(page, 'raw'), (data) ->
      html = $("<div/>").html(data)
      h1 = $("div.section h1:contains('#{name}')", html)

      options =
        dialogClass: 'ui-corner-all'
        modal:      true
        minHeight:  100
        height:     'auto'
        width: 'auto'
        position: [0,10000]
        close: -> usdlc.closeDialog()

      box = usdlc.dialog("<div/>", options)
      if h1.size() == 1
        title = single box, h1
      else
        title = tabbed box, h1
      usdlc.activateHtml(box)
      openDialogs.push box

      box.dialog("option", "title", title)
      $('.editable', box).removeClass('editable')
      after box

  single = (box, heading) ->
    html = heading.nextAll()
    box.append html
    return getTitle(html, heading)[0]

  tabno = 0

  tabbed = (box, headings) ->
    tabPanel = $ '<div/>', class:"tab-container"
    box.append tabPanel
    tabs = $ '<ul/>', class:"etabs"
    tabPanel.append(tabs)
    title = ''
    headings.each (index) ->
      heading = $(this)
      html = heading.nextAll()
      [title, tabName] = getTitle(html, heading)
      id = "tabs-#{tabno++}"
      tab = $ '<li/>', class:"tab"
      a = $ '<a/>', href: '#' + id
      a.text tabName
      tab.append a
      tabs.append tab
      body = $ '<div/>', id: id
      body.append heading.nextAll()
      tabPanel.append body
    tabPanel.easytabs()
    return title

  getTitle = (box, heading) ->
    h1 = $("h1", box)
    if h1.length
        title = h1.first().text()
        h1.detach()
    else
        title = heading.text()
    return title.split(':')
